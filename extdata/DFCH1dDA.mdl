# Made from DFCH1dNP.  Found the imperfect detection is not used
# 1. No capture histories detected for the list, so no imperfect detection estimation
#    for the list frame
# 2. Uses individual covariances (colony size) and data augmentation for the
#    new colony estimation.
# 3. Random effect for observer detection and including the ground truth (GT) observations
# 4. Also has the extra difficulty to first detect colonies.
# nCoef = (nYear - 1) + (nFrame - 1) + nStr = 5
var ColZ[nCapHistAug], Nest[nCapHistAug], Act[nCapHistAug], xCH[nCH, nCapHistAug], pCH[nCH, nCapHistAug], ToStr[4, nFS], pObsr[nObsr]
data{
  # Front observer = 1, Rear = 2, Ground = 3
  Front <- 1
  Rear <- 2
  Ground <- 3
}

model {
  # Observer counts, including ground counts.
  # Some aerial counts may be higher than the ground truth
  # These are the observations by the front, rear, and ground
  # observers.
  for (iObs in 1:nColObs) {
    # nest[iObs]~dbin(pObsr[Obs2Obsr[iObs]],Nest[Obs2Col[iObs]]);
    nest[iObs] ~ dpois(pObsr[Obs2Obsr[iObs]] * Nest[Obs2Col[iObs]])
    
    # act[iObs]~ dbin(pObsr[Obs2Obsr[iObs]], Act[Obs2Col[iObs]]);
    act[iObs] ~ dpois(pObsr[Obs2Obsr[iObs]] * Act[Obs2Col[iObs]])
  }
  
  # Colony detection from few capture histories.  The number of capture
  # histories may not reflect the number of observed, new colonies:
  # The detection protocol may not have been used on all the observed and
  # extra capture histories may have been done in the non-sample plots.
  for (iCapHist in 1:nCapHistAug) {
    # number of augmented capture histories
    # Capture history probability
    xCH[1:nCH, iCapHist] ~ dmulti(pCH[1:nCH, iCapHist], xCHTot[iCapHist])
    
    # Calculate the new colony detection for each observer given colony size
    pCO[1, iCapHist] <- (1 - (1 - pNest2Col * pObsr[Front]) ^ 
                         sqrt(Nest[iCapHist]))
    pCO[2, iCapHist] <- (1 - (1 - pNest2Col * pObsr[Rear]) ^ 
                         sqrt(Nest[iCapHist]))
    
    # Calculate the detection of combinations of different capture histories
    pCH[1, iCapHist] <- ColZ[iCapHist] * 
                         (1 - pCO[1, iCapHist]) * pCO[2, iCapHist]
    pCH[2, iCapHist] <- ColZ[iCapHist] * 
                         pCO[1, iCapHist] * (1 - pCO[2, iCapHist])
    pCH[3, iCapHist] <- ColZ[iCapHist] *   
                         pCO[1, iCapHist] * pCO[2, iCapHist]
    # Missed both didn't see or not there
    pCH[4, iCapHist] <- ColZ[iCapHist] * 
                         (1 - pCO[1, iCapHist]) * (1 - pCO[2, iCapHist]) +
                         (1 - ColZ[iCapHist])  
  }
  
  # True colonies and true nests, and active in colonies.
  # There is an estimate for each list, new, and augmented colony
  for (iCol in 1:nCapHistAug) {
    ColZ[iCol] ~ dbern(ColPsi[CapHist2FS[iCol]])
    Nest[iCol] ~ dpois(ColZ[iCol] * NpC[CapHist2FS[iCol]])
    Act[iCol] ~ dbin(pAct[CapHist2FS[iCol]], Nest[iCol])
  }
  
  # Occupancy of active colonies
  for (iFS in 1:nFS) {
    OccCol[iFS] = sum(ifelse(CapHist2FS == iFS, Nest > 0, 0))
    ActCol[iFS] = sum(ifelse(CapHist2FS == iFS, Act > 0, 0))
    NestTot[iFS] = sum(ifelse(CapHist2FS == iFS, Nest, 0))
    ActTot[iFS] = sum(ifelse(CapHist2FS == iFS, Act, 0))
  }
  
  # Area expansion for the area frame
  for (iYrStr in 1:nYrStr) {
    LstColTot[iYrStr] = OccCol[iYrStr]
    LstActColTot[iYrStr] = ActCol[iYrStr]
    LstNestTot[iYrStr] = NestTot[iYrStr]
    LstActTot[iYrStr] = ActTot[iYrStr]
    
    NewColTot[iYrStr] ~ dpois(ExpFac[iYrStr] * OccCol[nYrStr + iYrStr])
    NewActColTot[iYrStr] ~ dpois(ExpFac[iYrStr] * ActCol[nYrStr + iYrStr])
    NewNestTot[iYrStr] ~ dpois(ExpFac[iYrStr] * NestTot[nYrStr + iYrStr])
    NewActTot[iYrStr] ~ dpois(ExpFac[iYrStr] * ActTot[nYrStr + iYrStr])
  }
  
  # Factor between nest and colony detection
  pNest2Col ~ dbeta(1, 1)
  
  # Year-Frame-Stratum Occupancy rate random effect
  # where area frame includes data augmentation.
  ColPsiNu ~ dunif(0.5, 1000)
  ColPsiNup ~ dunif(0.5, 1000)
  for (iFS in 1:nFS) {
    ColPsi[iFS] ~ dbeta(ColPsiNu, ColPsiNup)
  }
  
  lnNpC <- ToStr %*% lnNpCFS
  for (iFS in 1:(2 * nYrStr)) {
    NpC[iFS] <- exp(lnNpC[iFS])
  }
  
  for (iFS in 1:4) {
    lnNpCFS[iFS] ~ dnorm(0, 0.01) # dunif(-10,10);
  }
  
  # Assume proportion active random effect
  # does not change between list and area frames
  pActNu  ~ dunif(0.5, 100)
  pActNup  ~ dunif(0.5, 100)
  for (iFS in 1:nFS) {
    pAct[iFS] ~ dbeta(pActNu, pActNup)
  }
  
  # Observer random effects
  # Front = 1, Rear = 2, Ground = 3
  # We have more informed priors from last year.
  pObsr[Front] ~ dbeta(2 * 0.98, 2 * 0.02)
  pObsr[Rear] ~ dbeta(2 * 0.85, 2 * 0.15)
  pObsr[Ground] <- 1.0  # Ground truth
}
